plugins {
    id 'java'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation platform(project(':bom'))
    implementation platform('org.springframework.boot:spring-boot-dependencies:3.5.6')

    implementation project(':core')
    implementation project(':api')
    implementation project(':service')

    implementation 'org.springframework.data:spring-data-jdbc'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.flywaydb:flyway-database-postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation 'org.springframework:spring-webmvc'
    testImplementation 'jakarta.servlet:jakarta.servlet-api'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jar {
    enabled = false
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    shouldRunAfter(tasks.test)
    useJUnitPlatform()
    testLogging {
        events 'passed', 'failed', 'skipped'
    }
}

tasks.check {
    dependsOn tasks.named('integrationTest')
}

test {
    useJUnitPlatform()
}
